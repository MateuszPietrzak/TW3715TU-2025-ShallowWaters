{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "063b011e-f81a-4921-b076-4b3f05e9694f",
   "metadata": {},
   "source": [
    "# Literature report experiments notebook\n",
    "\n",
    "This notebook is the output of our initial study during the literature report part of the project.\n",
    "It implements numerical solution to a 1D wave equation with cubic damping using Harmonic Balance method\n",
    "and Finite difference method for comparison of the two."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "ff20d57e-534c-436a-a326-e655d939005a",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit;\n",
    "using MethodOfLines;\n",
    "using DifferentialEquations;\n",
    "using DomainSets;\n",
    "using FFTW;\n",
    "using CairoMakie;\n",
    "using LinearAlgebra;\n",
    "using BoundaryValueDiffEq;\n",
    "using SparseArrays;\n",
    "using NonlinearSolve;\n",
    "using GLMakie;\n",
    "include(\"helper_functions.jl\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "55fd6c57-4258-454a-b759-67b306d35642",
   "metadata": {},
   "outputs": [],
   "source": [
    "xleft::Float64 = 0.0;\n",
    "xright::Float64 = 1.0;\n",
    "t0::Float64 = 0.0;\n",
    "T::Float64 = 150.0;\n",
    "dt = 0.01;\n",
    "Nx = 100;\n",
    "Nt = T / dt;\n",
    "order = 2;\n",
    "step = (xright - xleft)/Nx;\n",
    "grid = collect(xleft:step:xright);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "253d05db-d4ac-433d-8323-c56e6601af75",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Definition of constants (placeholders):\n",
    "gamma::Float64 = 0.0;\n",
    "gamma_3::Float64 = 2.0;\n",
    "c::Float64 = 3;\n",
    "omega = 40;\n",
    "A_forcing::Float64 = 1000;\n",
    "lambda_forcing::Float64 = -40;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "d1ccf965-1a0e-4af8-827d-bc736c413da5",
   "metadata": {},
   "outputs": [],
   "source": [
    "@parameters x,t\n",
    "Dx = Differential(x);\n",
    "Dt = Differential(t);\n",
    "Dtt = Differential(t)^2;\n",
    "Dxx = Differential(x)^2;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "479817a1-3f75-4f1e-8ac7-253128a3fe55",
   "metadata": {},
   "source": [
    "## Harmonic Balance\n",
    "To further delve into Julia ecosystem the first part is symbollic calculation, which calculates the coeffitients\n",
    "for the $\\sin$ and $\\cos$ functions in first order Harmonic Balance approximation. Then, the equations are \n",
    "discretized using a method found in `helper_functions.jl` file, and solved."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "34568765-2934-43d8-99e4-f49133adac92",
   "metadata": {},
   "outputs": [],
   "source": [
    "@variables A(..), B(..);\n",
    "r1 = @rule cos(~x)^3 => 0.75 * cos(~x) + 0.25 * cos(3 * ~x);\n",
    "r2 = @rule sin(~x)^3 => 0.75 * sin(~x) - 0.25 * sin(3 * ~x);\n",
    "r3 = @rule cos(~x)^2 => 1 - sin(~x)^2;\n",
    "r4 = @rule sin(~x)^2 => 1 - cos(~x)^2;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "a942b343-09c9-4fbf-ac39-adc0a37c95a8",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Trial substitution:\n",
      "1600.0*A_array[i_local] + 9.0*(A_array[i_local+1] - 2*A_array[i_local] + A_array[i_local-1]) / dx^2 + 96000.0*((A_array[i_local])^2)*B_array[i_local] + 96000.0*((B_array[i_local])^3)\n",
      "Solved\n",
      "  8.318055 seconds (23.11 M allocations: 1.167 GiB, 2.61% gc time, 98.28% compilation time: 13% of which was recompilation)\n"
     ]
    }
   ],
   "source": [
    "@time begin\n",
    "    u = A(x) * sin(omega*t) + B(x) * cos(omega*t)\n",
    "    \n",
    "    y = Dt(Dt(u)) - c*c*Dx(Dx(u)) + gamma*Dt(u) + gamma_3*Dt(u)*Dt(u)*Dt(u)\n",
    "    y_exp = expand_derivatives(y)\n",
    "    \n",
    "    y_exp = simplify(expand(y_exp), RuleSet([r1, r2, r3, r4]))\n",
    "    y_exp = expand(y_exp)\n",
    "    y_exp = simplify(y_exp, RuleSet([r1, r2, r3, r4]))\n",
    "    \n",
    "    sin_coeff = -Symbolics.coeff(y_exp, sin(omega*t));\n",
    "    cos_coeff = -Symbolics.coeff(y_exp, cos(omega*t));\n",
    "    \n",
    "    println(\"Trial substitution:\");\n",
    "    println(transform_sym(sin_coeff));\n",
    "    \n",
    "    sin_eq_str = transform_sym(sin_coeff)\n",
    "    cos_eq_str = transform_sym(cos_coeff)\n",
    "    \n",
    "    nonlinear_residual = create_residual_function_1D(Nx, sin_eq_str, cos_eq_str)\n",
    "    initial_guess = ones(2*(Nx+1))\n",
    "    \n",
    "    prob = NonlinearProblem(nonlinear_residual, initial_guess, step)\n",
    "    sol = NonlinearSolve.solve(prob, NewtonRaphson(), reltol = 1e-5, abstol = 1e-5);\n",
    "    \n",
    "    println(\"Solved\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "37adccea-76cf-4ed5-b7ea-1a4950cd4796",
   "metadata": {},
   "outputs": [],
   "source": [
    "myA  = sol.u[1:Nx+1];\n",
    "myB  = sol.u[Nx+2:end];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "d5ad9c5a-e37c-43b4-9d25-e377919fc32d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "101-element Vector{Float64}:\n",
       "  1.0749307269579403e-16\n",
       " -0.03724533808630602\n",
       " -0.06298567688779776\n",
       " -0.07749540157630355\n",
       " -0.08186029967678551\n",
       " -0.07781117060029112\n",
       " -0.06742115797756854\n",
       " -0.0527900677416504\n",
       " -0.03579961272425125\n",
       " -0.017972956830905953\n",
       " -0.00043190516864436915\n",
       "  0.016075693205635164\n",
       "  0.031109856913968892\n",
       "  ⋮\n",
       " -0.03159230970181645\n",
       " -0.03156811084898078\n",
       " -0.030781185606229545\n",
       " -0.029270486956671005\n",
       " -0.027093739660105892\n",
       " -0.024323075269876558\n",
       " -0.021040328148977373\n",
       " -0.017332565234908063\n",
       " -0.013288362804179894\n",
       " -0.008995206730159442\n",
       " -0.0045381958159488485\n",
       " -7.655521287414221e-18"
      ]
     },
     "execution_count": 8,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "myA"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "05defc69-aed5-4ca5-8763-8d655b42b537",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "frames = 500\n",
    "\n",
    "# Observable for u(x,t)\n",
    "u = Observable(myA .* sin(0.0) .+ myB .* cos(0.0))\n",
    "\n",
    "# Create figure and axis\n",
    "fig = Figure(size = (800, 500))\n",
    "ax = Axis(fig[1, 1],\n",
    "    title = \"t = 0.0\",\n",
    "    xlabel = \"x\",\n",
    "    ylabel = \"u(x, t)\",\n",
    "    limits = (nothing, nothing, -0.5, 0.5)\n",
    ")\n",
    "\n",
    "# Plot linked to observable\n",
    "lines!(ax, grid, u)\n",
    "\n",
    "record(fig, \"figures/1D_HB_wave.gif\", 1:frames; framerate = 60) do i\n",
    "    t = i * dt\n",
    "    u[] = myA .* sin(omega * t) .+ myB .* cos(omega * t)\n",
    "    ax.title = \"t = $(round(t, digits = 2))\"\n",
    "end\n",
    "\n",
    "display(\"image/gif\", read(\"figures/1D_HB_wave.gif\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "05924ff7-95ef-4776-aeb0-dd3753fd8204",
   "metadata": {},
   "source": [
    "## "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d7d0d130-f193-4dd7-957d-15528ead6e94",
   "metadata": {},
   "source": [
    "As will be apparent from the next part of the notebook, this solution is very simillar to the Backwards Euler solution visually, but to further examine that those solutions match,\n",
    "the next graph shows a spatial Fourier transform of the wave (as it is a standing one), to show the underlying frequencies."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "86e88f7a-dcf9-428f-ab76-e9f100d204af",
   "metadata": {},
   "outputs": [],
   "source": [
    "k = 1339\n",
    "\n",
    "fs = 1/step\n",
    "\n",
    "new_u = myA * sin(omega * (k * dt)) .+ myB * cos(omega * (k * dt))\n",
    "\n",
    "F = fftshift(fft(new_u))\n",
    "freqs =  fftshift(fftfreq(length(new_u), fs))\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax1 = Axis(f[1, 1],\n",
    "    xlabel = \"Distance (m)\",\n",
    "    ylabel = \"Displacement (m)\",\n",
    ")\n",
    "ax2 = Axis(f[2, 1], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "# positions of the dashed lines\n",
    "peak_freq = freqs[argmax(abs.(F))]\n",
    "vline_positions = [-peak_freq, peak_freq]\n",
    "# plot the lines\n",
    "lines!(ax1, grid, new_u)\n",
    "lines!(ax2, freqs, abs.(F) / length(new_u))\n",
    "vlines!(ax2, vline_positions; linestyle = :dash, linewidth = 2, color = :orange)\n",
    "# set x-limits\n",
    "xlims!(ax2, -10, 10)\n",
    "tick_positions = [-10, -5, -peak_freq, 0, peak_freq, 5, 10]\n",
    "tick_labels = [\"-10\", \"-5\", string(round(-peak_freq, digits=2)), \"0\", string(round(peak_freq, digits=2)), \"5\", \"10\"]\n",
    "ax2.xticks = (tick_positions, tick_labels)\n",
    "\n",
    "save(\"figures/space_fourier_HB_linear.png\", f)\n",
    "f"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e86a1a12-55af-4269-ba9a-cde654f90768",
   "metadata": {},
   "outputs": [],
   "source": [
    "u_at_point = Float64[]\n",
    "\n",
    "for i in 0:dt:T - dt\n",
    "    push!(u_at_point, myA[50] * sin(omega * i) + myB[50] * cos(omega * i))\n",
    "end\n",
    "\n",
    "fs_time = 1/dt\n",
    "N = length(u_at_point)\n",
    "\n",
    "F = fftshift(fft(u_at_point))\n",
    "freqs = fftshift(fftfreq(N, fs_time))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax2 = Axis(f[1, 2],\n",
    "    xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "ax1 =Axis(f[1, 1],\n",
    "    ylabel = \"Amplitude (m)\",\n",
    "    xlabel = \"Time (s)\",\n",
    ")\n",
    "peak_freq = freqs[argmax(abs.(F))]\n",
    "vline_positions = [-peak_freq, peak_freq]\n",
    "\n",
    "# Correct normalization: divide by N, not fs_time\n",
    "lines!(ax1, 0:dt:(T -dt), u_at_point)\n",
    "lines!(ax2, freqs, abs.(F) ./ N)\n",
    "xlims!(ax2, -10, 10)\n",
    "\n",
    "save(\"figures/time_fourier_HB_linear.png\", f)\n",
    "\n",
    "println(\"Maximum of \", maximum(abs.(F))./ length(u_at_point), \" at frequency of \", abs.(peak_freq))\n",
    "\n",
    "\n",
    "f"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "257568a4-a655-42a9-802a-5537ebf9ccf1",
   "metadata": {},
   "source": [
    "## Finite Difference (Backwards Euler time-integration)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68e93ecb-794a-44b4-9dd0-6c55a7691857",
   "metadata": {},
   "outputs": [],
   "source": [
    "@variables u(..)\n",
    "\n",
    "#Define source function (placeholder constant spatial term):\n",
    "source_f(x, t) = A_forcing * exp(lambda_forcing*(x)^2) * sin(omega*t)\n",
    "\n",
    "#Define initial condition (placeholder sinusoidal):\n",
    "#u0(x, t) = exp(-(35*pi)*(x-0.5)^2)\n",
    "u0(x, t) = 0\n",
    "v0(x) = 0\n",
    "\n",
    "\n",
    "#Define equation (with and without cubic damping term):\n",
    "equation = [Dtt(u(x,t)) + gamma*Dt(u(x,t)) + gamma_3*(Dt(u(x,t)))^3 ~ c^2*Dxx(u(x,t)) + source_f(x, t)]\n",
    "#equation = [Dtt(u(x,t)) ~ c^2*Dxx(u(x,t))]\n",
    "\n",
    "\n",
    "#Define domains:\n",
    "domains = [x ∈ Interval(xleft, xright), t ∈ Interval(t0, T)]\n",
    "\n",
    "#Define boundary conditions:\n",
    "bound_conditions = [u(x, 0) ~ u0(x, 0),\n",
    "                     u(xleft, t) ~ 0,\n",
    "                     u(xright, t) ~ 0,\n",
    "                     Dt(u(x, 0)) ~ v0(x)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d147794d-a6f1-4e42-8701-a94d4dad743f",
   "metadata": {},
   "outputs": [],
   "source": [
    "@time begin\n",
    "    \n",
    "    #Create PDE system and assign identifier (order matters: eq, bcs, domains, paramers, variables; format parameters and variables as lists, provide arguments whenever they exist):\n",
    "    @named pde_sys = PDESystem(equation, bound_conditions, domains, [x, t], [u(x,t)]);\n",
    "    \n",
    "    #Define a discretization (first argument specifies parameters to discretize, second indicates parameters to keep continuous, third provides the approximation order):\n",
    "    discretization = MOLFiniteDifference([x=>grid], t, approx_order=order);\n",
    "    \n",
    "    #Proceed with discretization:\n",
    "    @time eq_discretized = discretize(pde_sys,discretization);\n",
    "    \n",
    "    #Solve the resulting system (specified solver dictates whether or not time marching is used, as well as the method: Euler(), ImplicitEuler(), or Trapezoidal(), all with given time steps):\n",
    "    #Warning: if the time step is not explicitly given, solver may default to actual method of lines, instead of time marching\\\n",
    "    dt = 1e-2;\n",
    "    solution= solve(eq_discretized, ImplicitEuler(), dt=dt, saveat=0.01);\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ea7174e5-880f-4f71-9341-e46962e0eb75",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Plot results:\n",
    "discrete_t = solution.t\n",
    "\n",
    "sol_u = solution[u(x, t)]\n",
    "\n",
    "Nx = size(sol_u, 1)\n",
    "Nt = length(solution.t)\n",
    "\n",
    "# Observable for u(x,t)\n",
    "live = Observable(sol_u[:, 1])\n",
    "\n",
    "# Create figure and axis\n",
    "fig = Figure(size = (800, 500))\n",
    "ax = Axis(fig[1, 1],\n",
    "    title = \"t = 0.0\",\n",
    "    xlabel = \"x\",\n",
    "    ylabel = \"u(x, t)\",\n",
    "    limits = (nothing, nothing, -0.5, 0.5)\n",
    ")\n",
    "\n",
    "# Plot linked to observable\n",
    "lines!(ax, grid, live)\n",
    "\n",
    "frames = Nt\n",
    "\n",
    "record(fig, \"figures/1D_FD_wave.gif\", 1:500; framerate = 60) do i\n",
    "    live[] = sol_u[:,i]\n",
    "    ax.title = \"t = $(round(discrete_t[i], digits = 2))\"\n",
    "end\n",
    "\n",
    "display(\"image/gif\", read(\"figures/1D_FD_wave.gif\"))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "21776ad0-3535-48f2-b7fb-836605633e12",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Fourier Transform analysis\n",
    "\n",
    "k = 1000\n",
    "fs = 1/step\n",
    "\n",
    "u_at_k = sol_u[1:end, k]\n",
    "\n",
    "F = fftshift(fft(u_at_k))\n",
    "freqs =  fftshift(fftfreq(length(u_at_k), fs))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax1 = Axis(f[1, 1],\n",
    "    xlabel = \"Distance (m)\",\n",
    "    ylabel = \"Displacement (m)\",\n",
    ")\n",
    "\n",
    "ax2 = Axis(f[2, 1], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "\n",
    "peak_freq = freqs[argmax(abs.(F))]\n",
    "vline_positions = [-peak_freq, peak_freq]\n",
    "\n",
    "lines!(ax1, grid, u_at_k)\n",
    "lines!(ax2, freqs, abs.(F) / maximum(abs.(F)))\n",
    "xlims!(ax2, -10, 10)\n",
    "\n",
    "vlines!(ax2, vline_positions; linestyle = :dash, linewidth = 2, color = :orange)\n",
    "# set x-limits\n",
    "xlims!(ax2, -10, 10)\n",
    "tick_positions = [-10, -5, -peak_freq, 0, peak_freq, 5, 10]\n",
    "tick_labels = [\"-10\", \"-5\", string(round(-peak_freq, digits=2)), \"0\", string(round(peak_freq, digits=2)), \"5\", \"10\"]\n",
    "ax2.xticks = (tick_positions, tick_labels)\n",
    "\n",
    "save(\"figures/space_fourier_time_march_linear.png\", f)\n",
    "\n",
    "f"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71c7cfa9-aa19-4ca3-ba5c-78d72b39b950",
   "metadata": {},
   "outputs": [],
   "source": [
    "delta = 10000\n",
    "u_at_point = sol_u[50, delta:end]\n",
    "\n",
    "fs_time = fs = 1/dt\n",
    "\n",
    "F = fftshift(fft(u_at_point))\n",
    "freqs =  fftshift(fftfreq(length(u_at_point), fs_time))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax2 = Axis(f[1, 2], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "ax1 = Axis(f[1, 1], xlabel = \"Time (s)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "\n",
    "peak_freq = freqs[argmax(abs.(F))]\n",
    "vline_positions = [-peak_freq, peak_freq]\n",
    "# vlines!(ax2, vline_positions; linestyle = :dash, linewidth = 2, color = :orange)\n",
    "\n",
    "lines!(ax1, 0:dt:(T - delta*dt + dt), u_at_point)\n",
    "lines!(ax2, freqs, abs.(F) ./ length(u_at_point))\n",
    "xlims!(ax2, -10, 10)\n",
    "\n",
    "tick_positions = [0, 20, 40]\n",
    "tick_labels = [\"100\", \"120\", \"140\"]\n",
    "ax1.xticks = (tick_positions, tick_labels)\n",
    "\n",
    "\n",
    "save(\"figures/time_fourier_time_march_linear.png\", f)\n",
    "\n",
    "f\n",
    "println(\"Maximum of \", maximum(abs.(F))./ length(u_at_point), \" at frequency of \", abs.(peak_freq))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b6a03569-cafd-432a-b3a1-36ff75407540",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
