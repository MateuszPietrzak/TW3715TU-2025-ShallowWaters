{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "063b011e-f81a-4921-b076-4b3f05e9694f",
   "metadata": {},
   "source": [
    "# Literature report experiments notebook\n",
    "\n",
    "This notebook is the output of our initial study during the literature report part of the project.\n",
    "It implements numerical solution to a 1D wave equation with cubic damping using Harmonic Balance method\n",
    "and Finite difference method for comparison of the two."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ff20d57e-534c-436a-a326-e655d939005a",
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit\n",
    "using MethodOfLines\n",
    "using DifferentialEquations\n",
    "using DomainSets\n",
    "using FFTW\n",
    "using CairoMakie\n",
    "using LinearAlgebra\n",
    "using BoundaryValueDiffEq\n",
    "using SparseArrays\n",
    "using NonlinearSolve\n",
    "using GLMakie\n",
    "include(\"helper_functions.jl\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "55fd6c57-4258-454a-b759-67b306d35642",
   "metadata": {},
   "outputs": [],
   "source": [
    "xleft::Float64 = 0.0;\n",
    "xright::Float64 = 1.0;\n",
    "t0::Float64 = 0.0;\n",
    "T::Float64 = 70.0;\n",
    "Nx = 100;\n",
    "order = 2;\n",
    "step = (xright - xleft)/Nx;\n",
    "grid = collect(xleft:step:xright);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "253d05db-d4ac-433d-8323-c56e6601af75",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Definition of constants (placeholders):\n",
    "gamma::Float64 = 0.25;\n",
    "gamma_3::Float64 = 0.001;\n",
    "c::Float64 = 3;\n",
    "omega::Float64 = 40;\n",
    "A_forcing::Float64 = 101;\n",
    "lambda_forcing::Float64 = -40;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d1ccf965-1a0e-4af8-827d-bc736c413da5",
   "metadata": {},
   "outputs": [],
   "source": [
    "@parameters x,t\n",
    "Dx = Differential(x);\n",
    "Dt = Differential(t);\n",
    "Dtt = Differential(t)^2;\n",
    "Dxx = Differential(x)^2;"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "479817a1-3f75-4f1e-8ac7-253128a3fe55",
   "metadata": {},
   "source": [
    "## Harmonic Balance\n",
    "To further delve into Julia ecosystem the first part is symbollic calculation, which calculates the coeffitients\n",
    "for the $\\sin$ and $\\cos$ functions in first order Harmonic Balance approximation. Then, the equations are \n",
    "discretized using a method found in `helper_functions.jl` file, and solved."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "34568765-2934-43d8-99e4-f49133adac92",
   "metadata": {},
   "outputs": [],
   "source": [
    "@variables A(..), B(..)\n",
    "r1 = @rule cos(~x)^3 => 0.75 * cos(~x) + 0.25 * cos(3 * ~x)\n",
    "r2 = @rule sin(~x)^3 => 0.75 * sin(~x) - 0.25 * sin(3 * ~x)\n",
    "r3 = @rule cos(~x)^2 => 1 - sin(~x)^2\n",
    "r4 = @rule sin(~x)^2 => 1 - cos(~x)^2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a942b343-09c9-4fbf-ac39-adc0a37c95a8",
   "metadata": {},
   "outputs": [],
   "source": [
    "@time begin\n",
    "    u = A(x) * sin(omega*t) + B(x) * cos(omega*t)\n",
    "    \n",
    "    y = Dt(Dt(u)) - c*c*Dx(Dx(u)) + gamma*Dt(u) + gamma_3*Dt(u)*Dt(u)*Dt(u)\n",
    "    y_exp = expand_derivatives(y)\n",
    "    \n",
    "    y_exp = simplify(expand(y_exp), RuleSet([r1, r2, r3, r4]))\n",
    "    y_exp = expand(y_exp)\n",
    "    y_exp = simplify(y_exp, RuleSet([r1, r2, r3, r4]))\n",
    "    \n",
    "    sin_coeff = -Symbolics.coeff(y_exp, sin(omega*t));\n",
    "    cos_coeff = -Symbolics.coeff(y_exp, cos(omega*t));\n",
    "    \n",
    "    println(\"Trial substitution:\");\n",
    "    println(transform_sym(sin_coeff));\n",
    "    \n",
    "    sin_eq_str = transform_sym(sin_coeff)\n",
    "    cos_eq_str = transform_sym(cos_coeff)\n",
    "    \n",
    "    nonlinear_residual = create_residual_function_1D(Nx, sin_eq_str, cos_eq_str)\n",
    "    initial_guess = ones(2*(Nx+1))\n",
    "    \n",
    "    prob = NonlinearProblem(nonlinear_residual, initial_guess, step)\n",
    "    sol = NonlinearSolve.solve(prob, NewtonRaphson(), reltol = 1e-5, abstol = 1e-5);\n",
    "    \n",
    "    println(\"Solved\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "37adccea-76cf-4ed5-b7ea-1a4950cd4796",
   "metadata": {},
   "outputs": [],
   "source": [
    "myA  = sol.u[1:Nx+1];\n",
    "myB  = sol.u[Nx+2:end];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "05defc69-aed5-4ca5-8763-8d655b42b537",
   "metadata": {},
   "outputs": [],
   "source": [
    "Nt = 5\n",
    "dt = 0.01\n",
    "frames = Int(Nt/dt)\n",
    "\n",
    "# Observable for u(x,t)\n",
    "u = Observable(myA .* sin(0.0) .+ myB .* cos(0.0))\n",
    "\n",
    "# Create figure and axis\n",
    "fig = Figure(size = (800, 500))\n",
    "ax = Axis(fig[1, 1],\n",
    "    title = \"t = 0.0\",\n",
    "    xlabel = \"x\",\n",
    "    ylabel = \"u(x, t)\",\n",
    "    limits = (nothing, nothing, -0.5, 0.5)\n",
    ")\n",
    "\n",
    "# Plot linked to observable\n",
    "lines!(ax, grid, u)\n",
    "\n",
    "record(fig, \"1D_HB_wave.gif\", 1:frames; framerate = 60) do i\n",
    "    t = i * dt\n",
    "    u[] = myA .* sin(omega * t) .+ myB .* cos(omega * t)\n",
    "    ax.title = \"t = $(round(t, digits = 2))\"\n",
    "end\n",
    "\n",
    "display(\"image/gif\", read(\"1D_HB_wave.gif\"))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d7d0d130-f193-4dd7-957d-15528ead6e94",
   "metadata": {},
   "source": [
    "As will be apparent from the next part of the notebook, this solution is very simillar to the Backwards Euler solution visually, but to further examine that those solutions match,\n",
    "the next graph shows a spatial Fourier transform of the wave (as it is a standing one), to show the underlying frequencies."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "86e88f7a-dcf9-428f-ab76-e9f100d204af",
   "metadata": {},
   "outputs": [],
   "source": [
    "k = 58\n",
    "dt = 0.01\n",
    "\n",
    "fs = Nx\n",
    "\n",
    "new_u = myA * sin(omega * (k * dt)) .+ myB * cos(omega * (k * dt))\n",
    "\n",
    "F = fftshift(fft(new_u))\n",
    "freqs =  fftshift(fftfreq(length(new_u), fs))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax1 = Axis(f[1, 1],\n",
    "    xlabel = \"Distance (m)\",\n",
    "    ylabel = \"Displacement (m)\",\n",
    ")\n",
    "\n",
    "ax2 = Axis(f[2, 1], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "\n",
    "# plot the lines\n",
    "lines!(ax1, grid, new_u)\n",
    "lines!(ax2, freqs, abs.(F) / fs)\n",
    "\n",
    "\n",
    "# update the layout\n",
    "xlims!(ax2, -10, 10)\n",
    "\n",
    "f"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "257568a4-a655-42a9-802a-5537ebf9ccf1",
   "metadata": {},
   "source": [
    "## Finite Difference (Backwards Euler time-integration)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "68e93ecb-794a-44b4-9dd0-6c55a7691857",
   "metadata": {},
   "outputs": [],
   "source": [
    "@variables u(..)\n",
    "\n",
    "#Define source function (placeholder constant spatial term):\n",
    "source_f(x, t) = A_forcing * exp(lambda_forcing*(x)^2) * sin(omega*t)\n",
    "\n",
    "#Define initial condition (placeholder sinusoidal):\n",
    "#u0(x, t) = exp(-(35*pi)*(x-0.5)^2)\n",
    "u0(x, t) = 0\n",
    "v0(x) = 0\n",
    "\n",
    "\n",
    "#Define equation (with and without cubic damping term):\n",
    "equation = [Dtt(u(x,t)) + gamma*Dt(u(x,t)) + gamma_3*(Dt(u(x,t)))^3 ~ c^2*Dxx(u(x,t)) + source_f(x, t)]\n",
    "#equation = [Dtt(u(x,t)) ~ c^2*Dxx(u(x,t))]\n",
    "\n",
    "\n",
    "#Define domains:\n",
    "domains = [x ∈ Interval(xleft, xright), t ∈ Interval(t0, T)]\n",
    "\n",
    "#Define boundary conditions:\n",
    "bound_conditions = [u(x, 0) ~ u0(x, 0),\n",
    "                     u(xleft, t) ~ 0,\n",
    "                     u(xright, t) ~ 0,\n",
    "                     Dt(u(x, 0)) ~ v0(x)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d147794d-a6f1-4e42-8701-a94d4dad743f",
   "metadata": {},
   "outputs": [],
   "source": [
    "@time begin\n",
    "    \n",
    "    #Create PDE system and assign identifier (order matters: eq, bcs, domains, paramers, variables; format parameters and variables as lists, provide arguments whenever they exist):\n",
    "    @named pde_sys = PDESystem(equation, bound_conditions, domains, [x, t], [u(x,t)]);\n",
    "    \n",
    "    #Define a discretization (first argument specifies parameters to discretize, second indicates parameters to keep continuous, third provides the approximation order):\n",
    "    discretization = MOLFiniteDifference([x=>grid], t, approx_order=order);\n",
    "    \n",
    "    #Proceed with discretization:\n",
    "    @time eq_discretized = discretize(pde_sys,discretization);\n",
    "    \n",
    "    #Solve the resulting system (specified solver dictates whether or not time marching is used, as well as the method: Euler(), ImplicitEuler(), or Trapezoidal(), all with given time steps):\n",
    "    #Warning: if the time step is not explicitly given, solver may default to actual method of lines, instead of time marching\\\n",
    "    dt = 1e-2;\n",
    "    solution= solve(eq_discretized, ImplicitEuler(), dt=dt, saveat=0.01);\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ea7174e5-880f-4f71-9341-e46962e0eb75",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Plot results:\n",
    "discrete_t = solution.t\n",
    "\n",
    "sol_u = solution[u(x, t)]\n",
    "\n",
    "Nx = size(sol_u, 1)\n",
    "Nt = length(solution.t)\n",
    "\n",
    "# Observable for u(x,t)\n",
    "live = Observable(sol_u[:, 1])\n",
    "\n",
    "# Create figure and axis\n",
    "fig = Figure(size = (800, 500))\n",
    "ax = Axis(fig[1, 1],\n",
    "    title = \"t = 0.0\",\n",
    "    xlabel = \"x\",\n",
    "    ylabel = \"u(x, t)\",\n",
    "    limits = (nothing, nothing, -0.5, 0.5)\n",
    ")\n",
    "\n",
    "# Plot linked to observable\n",
    "lines!(ax, grid, live)\n",
    "\n",
    "frames = Nt\n",
    "\n",
    "record(fig, \"1D_FD_wave.gif\", 1:500; framerate = 60) do i\n",
    "    live[] = sol_u[:,i]\n",
    "    ax.title = \"t = $(round(discrete_t[i], digits = 2))\"\n",
    "end\n",
    "\n",
    "display(\"image/gif\", read(\"1D_FD_wave.gif\"))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "21776ad0-3535-48f2-b7fb-836605633e12",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Fourier Transform analysis\n",
    "\n",
    "k = 1500\n",
    "fs = Nx\n",
    "\n",
    "u_at_k = sol_u[1:end, k]\n",
    "\n",
    "F = fftshift(fft(u_at_k))\n",
    "freqs =  fftshift(fftfreq(length(u_at_k), fs))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax1 = Axis(f[1, 1],\n",
    "    xlabel = \"Distance (m)\",\n",
    "    ylabel = \"Displacement (m)\",\n",
    ")\n",
    "\n",
    "ax2 = Axis(f[2, 1], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "\n",
    "lines!(ax1, grid, u_at_k)\n",
    "lines!(ax2, freqs, abs.(F) / fs)\n",
    "xlims!(ax2, -50, 50)\n",
    "\n",
    "f"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "71c7cfa9-aa19-4ca3-ba5c-78d72b39b950",
   "metadata": {},
   "outputs": [],
   "source": [
    "u_at_point = sol_u[36, 6500:end]\n",
    "fs_time = Nt/T\n",
    "\n",
    "F = fftshift(fft(u_at_point))\n",
    "freqs =  fftshift(fftfreq(length(u_at_point), fs_time))\n",
    "\n",
    "# Plotting with Makie\n",
    "f = Figure()\n",
    "ax1 = Axis(f[1, 1], xlabel = \"Frequency (Hz)\",\n",
    "    ylabel = \"Amplitude (m)\",\n",
    ")\n",
    "\n",
    "lines!(ax1, freqs, abs.(F) / fs_time)\n",
    "xlims!(ax1, -50, 50)\n",
    "\n",
    "f"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6965ce71-8a9f-4149-8e6c-5e139c1cb459",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.7",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
