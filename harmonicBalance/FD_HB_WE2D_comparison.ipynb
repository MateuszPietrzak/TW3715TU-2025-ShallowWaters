{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3467daff-8073-481f-b10a-2cc5b8e91905",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Logging: global_logger\n",
    "import TerminalLoggers: TerminalLogger\n",
    "global_logger(TerminalLogger())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e2e4ddf4-e2f4-4978-90a3-2f2699aa330e",
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using SparseArrays\n",
    "using ModelingToolkit\n",
    "using MacroTools: @capture, postwalk, prewalk\n",
    "using DifferentialEquations\n",
    "using NonlinearSolve\n",
    "using Plots\n",
    "\n",
    "\n",
    "Nx = Ny = 75;\n",
    "harmonics = 1; # number of harmonics\n",
    "u0_HB = 0.005 * zeros((Nx+1) * (Ny+1) * harmonics * 2);\n",
    "order = 2;\n",
    "Nt = 5\n",
    "N = Nx + 1\n",
    "\n",
    "xleft::Float64 = 0.0\n",
    "xright::Float64 = 10.0\n",
    "yleft::Float64 = 0.0\n",
    "yright::Float64 = 10.0\n",
    "stepx = (xright-xleft)/(Nx)\n",
    "stepy = (yright-yleft)/(Ny)\n",
    "\n",
    "#Definition of constants (placeholders):\n",
    "gamma::Float64 = 0.\n",
    "gamma_3::Float64 = gamma3_final = 0.0\n",
    "c::Float64 = 0.5\n",
    "omega::Float64 = 1\n",
    "A_forcing::Float64 = 250;\n",
    "lambda_forcing::Float64 = -40;\n",
    "\n",
    "\n",
    "@parameters x, y, t\n",
    "@variables u(..)\n",
    "\n",
    "Dx = Differential(x)\n",
    "Dy = Differential(y)\n",
    "Dt = Differential(t)\n",
    "#Full PDE: Dt(Dt(u(x, y))) - c*c*(Dx(Dx(u(x, y))) + Dy(Dy(u(x, y)))) + gamma*Dt(u(x, y)) + gamma_3*Dt(u(x, y))*Dt(u(x, y))*Dt(u(x, y))\n",
    "#Reformat as ddu = y_eq\n",
    "y_eq = c*c*(Dx(Dx(u(x, y))) + Dy(Dy(u(x, y)))) - gamma*Dt(u(x, y)) - gamma_3*Dt(u(x, y))*Dt(u(x, y))*Dt(u(x, y));\n",
    "# y_eq = expand_derivatives(y_eq)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5d937671-b5e0-4fa2-bb78-1652c0027419",
   "metadata": {},
   "outputs": [],
   "source": [
    "function transform_sym(ex)\n",
    "    return prewalk(ex) do instr\n",
    "        if @capture(instr, Differential(x)(Differential(x)(s_(x, y))))\n",
    "            return :(($s[i+1, j]-2* $s[i, j]+$s[i-1, j])/dx^2)\n",
    "            elseif @capture(instr, Differential(y)(Differential(y)(s_(x, y))))\n",
    "            return :(($s[i, j+1]-2* $s[i, j]+$s[i, j-1])/dy^2)\n",
    "            elseif @capture(instr, Differential(x)(s_(x, y)))\n",
    "            return :(($s[i+1, j] - $s[i-1, j]) / (2*dx))\n",
    "            elseif @capture(instr, Differential(y)(s_(x, y)))\n",
    "            return :(($s[i, j+1] - $s[i, j-1]) / (2*dy))\n",
    "            elseif @capture(instr, Differential(t)(s_(x, y)))\n",
    "            return :($(Symbol(\"d$(s)_array\"))[i, j])\n",
    "            elseif @capture(instr, Differential(x, 2)(s_(x, y)))\n",
    "            return :(($s[i+1, j]-2* $s[i, j]+$s[i-1, j])/dx^2)\n",
    "            elseif @capture(instr, Differential(y, 2)(s_(x, y)))\n",
    "            return :(($s[i, j+1]-2* $s[i, j]+$s[i, j-1])/dy^2)\n",
    "            elseif @capture(instr, Differential(x, 1)(s_(x, y)))\n",
    "            return :(($s[i+1, j] - $s[i-1, j]) / (2*dx))\n",
    "            elseif @capture(instr, Differential(y, 1)(s_(x, y)))\n",
    "            return :(($s[i, j+1] - $s[i, j-1]) / (2*dy))\n",
    "            elseif @capture(instr, Differential(t, 1)(s_(x, y)))\n",
    "            return :($(Symbol(\"d$(s)_array\"))[i, j])\n",
    "            elseif @capture(instr, s_(x, y))\n",
    "            return :($(Symbol(\"$(s)_array\"))[i, j])\n",
    "            elseif @capture(instr, s_[i_, j_])\n",
    "            return :($(Symbol(\"$(s)_array\"))[$(i), $(j)])\n",
    "        end\n",
    "        return instr\n",
    "    end\n",
    "end\n",
    "println(y_eq)\n",
    "println(\"_____________________________________________\")\n",
    "println(transform_sym(Meta.parse(string(y_eq))))\n",
    "\n",
    "function create_ODE_function(N, y_expr)\n",
    "    function_code = quote\n",
    "        function secODE!(ddu, du, u, p, t)\n",
    "            dx, dy = p\n",
    "            grid_size = $N * $N\n",
    "\n",
    "            u_array = reshape(@view(u[1:grid_size]), $N, $N)\n",
    "            du_array = reshape(@view(du[1:grid_size]), $N, $N)\n",
    "            ddu_array = reshape(@view(ddu[1:grid_size]), $N, $N)\n",
    "\n",
    "            # BCs (Dirichlet for now):\n",
    "            ddu_array[1,:] .= 0; ddu_array[end,:] .= 0\n",
    "            ddu_array[:,1] .= 0; ddu_array[:,end] .= 0\n",
    "\n",
    "            # Inner points:\n",
    "            for i in 2:$(N-1)\n",
    "                for j in 2:$(N-1)\n",
    "                    ddu_array[i, j] = ($y_expr) + A_forcing * exp(lambda_forcing*(i * dx /10.0)^2) * sin(omega * t)\n",
    "                end\n",
    "            end\n",
    "\n",
    "            return ddu\n",
    "        end\n",
    "    end\n",
    "    return eval(function_code)\n",
    "end\n",
    "\n",
    "y_expr = transform_sym(Meta.parse(string(y_eq)))\n",
    "\n",
    "ODEfunc = create_ODE_function(N, y_expr)\n",
    "u0_FD = zeros(N*N)\n",
    "du0 = zeros(N*N)\n",
    "tspan = (0.0, 120.0)\n",
    "par = [stepx, stepy]\n",
    "prob = SecondOrderODEProblem(ODEfunc, du0, u0_FD, tspan, par)\n",
    "\n",
    "@time sol = solve(prob, Tsit5(), saveat=0.1, progress=true, progress_steps=200)\n",
    "\n",
    "tgrid = sol.t\n",
    "\n",
    "\n",
    "xgrid = range(xleft, xright, length=N)\n",
    "ygrid = range(yleft, yright, length=N)\n",
    "\n",
    "\n",
    "u_data = [reshape(sol.u[k].x[1], N, N) for k in eachindex(sol.t)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0218e64a-a6e2-4bea-a2ef-6102386d6f71",
   "metadata": {},
   "outputs": [],
   "source": [
    "anim = @animate for (i, t) in enumerate(tgrid)\n",
    "    heatmap(xgrid, ygrid, u_data[i],\n",
    "            color=:magma,\n",
    "            xlabel=\"x\", ylabel=\"y\",\n",
    "            title=\"u(x,y,t) at t=$t s\",\n",
    "            clims=(-600, 600),\n",
    "            aspect_ratio=1)\n",
    "end\n",
    "\n",
    "gif(anim, \"FD_sol.gif\", fps=25)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1eab752d-8277-4d8e-805d-756381e75b37",
   "metadata": {},
   "source": [
    "## Harmonic Balance Solution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3d52fcd8-51e3-43f4-98e0-821df94fa475",
   "metadata": {},
   "outputs": [],
   "source": [
    "import Symbolics\n",
    "using DomainSets\n",
    "include(\"multiharmonic_balance.jl\");\n",
    "include(\"jacobian2D.jl\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc4888cc-dc18-4de3-9679-7797f17dcafa",
   "metadata": {},
   "outputs": [],
   "source": [
    "function make_WaveEquation_2D(u, g0, height, gamma, gamma3, forcing)\n",
    "    pde::Symbolics.Num = Dt(Dt(u)) - g0*height*(Dx(Dx(u)) + Dy(Dy(u))) + gamma*Dt(u) + gamma3*Dt(u)*Dt(u)*Dt(u) - forcing;\n",
    "    return pde\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be205edb-dd25-4846-aec2-0e61ab4d89d6",
   "metadata": {},
   "outputs": [],
   "source": [
    "u0 = rand((Nx + 1) * (Ny + 1) * harmonics * 2)*.4;\n",
    "solHB = nothing \n",
    "u_hist = Vector{Float64}[]  # store history for prediction\n",
    "gamma3_hist = Float64[]\n",
    "\n",
    "\n",
    "vars, var_exprs, (u_HB,) = create_ansatz((x, y), t, omega, harmonics);\n",
    "    \n",
    "forcing = A_forcing * exp(lambda_forcing*(x/10.0)^2) * sin(omega * t)\n",
    "\n",
    "bc_conds = Dict(\n",
    "    :A1 => [:(0.0), 0.0],\n",
    "    :B1 => [:(0.0), 0.0],\n",
    "    :A2 => [:(0.0), 0.0],\n",
    "    :B2 => [:(0.0), 0.0],\n",
    "    :A3 => [:(0.0), 0.0],\n",
    "    :B3 => [:(0.0), 0.0],\n",
    ")\n",
    "\n",
    "gamma3 = 0.0\n",
    "dgamma3 = 0.1\n",
    "resid = nothing\n",
    "\n",
    "while gamma3 < gamma3_final\n",
    "    timed_out = false\n",
    "    println(\"Beginning the symbolics manipulation\")\n",
    "    flush(stdout)\n",
    "    \n",
    "    if length(u_hist) >= 2\n",
    "        u0 = u_hist[end] + (u_hist[end] - u_hist[end-1]) * \n",
    "             dgamma3 / (gamma3_hist[end] - gamma3_hist[end-1])\n",
    "    elseif length(u_hist) == 1\n",
    "        u0 = u_hist[end]\n",
    "    else\n",
    "        u0 = rand((Nx+1) * (Ny+1) * harmonics * 2)*.3\n",
    "    end\n",
    "    \n",
    "    \n",
    "    pde::Symbolics.Num = make_WaveEquation_2D(u_HB, c, c, gamma, gamma3, forcing);\n",
    "    \n",
    "\n",
    "    expanded = expand_trig_jl(pde, t, omega)\n",
    "    eqns = make_equations(expanded, harmonics, omega, t)\n",
    "    sym_eqs = map(transform_sym(Nx, Ny) ∘ Meta.parse ∘ string, eqns)\n",
    "    \n",
    "    resid = create_residual_function(sym_eqs, vars, Nx, Ny, bc_conds)\n",
    "    \n",
    "    residual! = eval(resid)\n",
    "    \n",
    "    DiffMat, LaplCoeff = create_jac_blocks_2D(eqns, var_exprs, harmonics)\n",
    "    \n",
    "    jacobian = create_jacobian_function_2D(Nx + 1, DiffMat, LaplCoeff, harmonics)\n",
    "    \n",
    "    # Create prototype by calling jacobian! once with dummy sparse matrix\n",
    "    n = (Nx + 1) *(Ny+1) * harmonics * 2\n",
    "\n",
    "    jac_prototype = spzeros(n, n)\n",
    "    jacobian!(jac_prototype, u0, [stepx, stepy])  # fills in the pattern\n",
    "    \n",
    "    func = NonlinearFunction(residual!; jac = jacobian!, jac_prototype = jac_prototype)\n",
    "    prob = NonlinearProblem(func, u0, [stepx, stepy])\n",
    "    \n",
    "    solHB = solve(prob, RobustMultiNewton(), reltol=1e-5, abstol=1e-5, maxiters=1000)\n",
    "\n",
    "    u0 = solHB.u\n",
    "    println(round(gamma3, sigdigits=6), \" \", solHB.retcode)\n",
    "    if solHB.retcode == ReturnCode.Success\n",
    "        println(\"Succeeded!\")\n",
    "        flush(stdout)\n",
    "        push!(u_hist, copy(solHB.u))\n",
    "        push!(gamma3_hist, gamma3)\n",
    "        dgamma3 = min(dgamma3 * 1.2, 0.02)\n",
    "        \n",
    "        gamma3 += dgamma3\n",
    "\n",
    "        if gamma3 > gamma3_final\n",
    "            gamma3 = gamma3_final\n",
    "            break\n",
    "        end\n",
    "        \n",
    "    else\n",
    "        println(\"Failed! Going back to the previous gamma and reducing the step!\")\n",
    "        flush(stdout)\n",
    "        gamma3 -= dgamma3\n",
    "        dgamma3 /= 10\n",
    "        gamma3 += dgamma3\n",
    "        dgamma3 < 1e-6 && break\n",
    "    end\n",
    "\n",
    "\n",
    "    \n",
    "end\n",
    "\n",
    "println(\"Starting now the FINAL iteration\")\n",
    "flush(stdout)\n",
    "\n",
    "pde::Symbolics.Num = make_WaveEquation_2D(u_HB, c, c, gamma, gamma3, forcing);\n",
    "    \n",
    "expanded = expand_trig_jl(pde, t, omega)\n",
    "eqns = make_equations(expanded, harmonics, omega, t)\n",
    "sym_eqs = map(transform_sym(Nx, Ny) ∘ Meta.parse ∘ string, eqns)\n",
    "\n",
    "resid = create_residual_function(sym_eqs, vars, Nx, Ny, bc_conds);\n",
    "\n",
    "println(\"Finished making the residual\")\n",
    "flush(stdout)\n",
    "\n",
    "\n",
    "residual! = eval(resid)\n",
    "\n",
    "DiffMat, LaplCoeff = create_jac_blocks_2D(eqns, var_exprs, harmonics)\n",
    "\n",
    "jacobian = create_jacobian_function_2D(Nx + 1, DiffMat, LaplCoeff, harmonics)\n",
    "\n",
    "println(\"Finsihed making the jacobian\")\n",
    "flush(stdout)\n",
    "\n",
    "# Create prototype by calling jacobian! once with dummy sparse matrix\n",
    "n = (Nx + 1) *(Ny+1) * harmonics * 2\n",
    "jac_prototype = spzeros(n, n)\n",
    "jacobian!(jac_prototype, u0, [stepx, stepy])  # fills in the pattern\n",
    "\n",
    "\n",
    "println(\"Defined the problem\")\n",
    "flush(stdout)\n",
    "\n",
    "func = NonlinearFunction(residual!; jac = jacobian!, jac_prototype = jac_prototype)\n",
    "\n",
    "# func = NonlinearFunction(residual!)\n",
    "\n",
    "prob = NonlinearProblem(func, u0, [stepx, stepy])\n",
    "\n",
    "println(\"Solving\")\n",
    "flush(stdout)\n",
    "\n",
    "\n",
    "@time solHB = solve(prob, NewtonRaphson(), reltol=1e-5, abstol=1e-5, maxiters=1000)\n",
    "\n",
    "print(solHB.retcode)\n",
    "\n",
    "coefficientsHB = [reshape(solHB.u[(k-1)*(Nx+1)*(Ny+1)+1:k*(Nx+1)*(Ny+1)], Nx+1, Ny+1) for k in 1:(2*harmonics)];"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c93c535d-7352-4859-bc64-6eaca957b21b",
   "metadata": {},
   "outputs": [],
   "source": [
    "maxlim  = maximum(coefficientsHB[1])*1.2\n",
    "x = range(0, 1, length=Nx+1)\n",
    "y = range(0, 1, length=Ny+1)\n",
    "\n",
    "T = 2π / omega\n",
    "n_frames = 500\n",
    "anim = @animate for t in range(0, T, length=n_frames)\n",
    "    u_new = coefficientsHB[1] * 0.0\n",
    "    j = 1\n",
    "    for i in 1:(2*harmonics)\n",
    "        if isodd(i)\n",
    "            u_new .+= coefficientsHB[i] .* sin(j * omega * t)\n",
    "        else\n",
    "            u_new .+= coefficientsHB[i] .* cos(j * omega * t)\n",
    "            j += 1\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    surface(x, y, u_new', \n",
    "        zlims=(-maxlim, maxlim), \n",
    "        clims=(-maxlim, maxlim),\n",
    "        xlabel=\"x\", ylabel=\"y\", zlabel=\"u\",\n",
    "    title = \"H=$harmonics, ω=$(round(omega, digits=1)), \" *\n",
    "            \"γ=$gamma, γ₃=$(round(gamma3, sigdigits=2))\\n\" *\n",
    "            \"Nx=$Nx, Ny=$Ny, t=$(round(t, digits=2)))\",\n",
    "        camera=(30, 30)  # viewing angle\n",
    "    )\n",
    "end\n",
    "gif(anim, \"harmonicBalancePlots/WaveEq_gino_$(harmonics)H.gif\", fps=60)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5b9cbd01-d192-4a3d-83e7-b4014da1c0ca",
   "metadata": {},
   "source": [
    "## Analysis"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ca3d0697-b665-46a4-a8b3-c4ecd44e9583",
   "metadata": {},
   "outputs": [],
   "source": [
    "fs_time = fs = 1/0.1\n",
    "t_steady0 = 500\n",
    "target = 1 * omega / (2 * pi)\n",
    "\n",
    "function generateComplexAmplitudeFDSpectrumMatrix(sol, target)\n",
    "    r1 = zeros(N, N)\n",
    "    rows = 0\n",
    "    println(rows)\n",
    "    for x_idx in 1:1:N\n",
    "        for y_idx in 1:1:N\n",
    "            u_at_point = [sol[t][x_idx, y_idx] for t in 1:length(sol)]\n",
    "            F = fftshift(fft(u_at_point))\n",
    "            freqs = fftshift(fftfreq(length(u_at_point), fs_time))\n",
    "            (_, idx) = findmin(abs.(freqs .- target))\n",
    "            r1[x_idx, y_idx] = abs(F[idx]) / (length(sol) - t_steady0)\n",
    "        end\n",
    "    end\n",
    "    r1\n",
    "end\n",
    "\n",
    "using FFTW\n",
    "fd_complex_spectrum = generateComplexAmplitudeFDSpectrumMatrix(u_data, target)\n",
    "heatmap(fd_complex_spectrum)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8f00dfb2-de28-4bd6-a81a-819cadc8c99b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# coefficientsHB = so/lutions\n",
    "println(size(coefficientsHB[1]))\n",
    "function generateComplexAmplitudeHBSpectrumMatrix(h)\n",
    "    r1 = zeros(N, N)\n",
    "    rows = 0\n",
    "    for x_idx in 1:1:N\n",
    "        for y_idx in 1:1:N\n",
    "            #f = h * omega / 2π\n",
    "            r1[x_idx, y_idx] = abs(0.5 * coefficientsHB[2 * h][x_idx, y_idx] - im * 0.5 * coefficientsHB[2 * h - 1][x_idx, y_idx])\n",
    "        end  \n",
    "    end\n",
    "    r1\n",
    "end\n",
    "hb_complex_spectrum = generateComplexAmplitudeHBSpectrumMatrix(1)\n",
    "heatmap(hb_complex_spectrum)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e79ffd96-d240-4461-a1d9-46b2001ae564",
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e4eba3dd-fe2c-42be-9108-320414b65bb3",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
